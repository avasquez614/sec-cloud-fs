apply plugin:'application'

mainClassName = 'org.avasquez.seccloudfs.filesystem.fuse.SecCloudFS'

dependencies {
    compile project(':commons')
    compile project(':processing')
    compile project(':google-drive')
    compile project(':dropbox')
    compile project(':amazon-s3')
    compile project(':apache-vfs')
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'net.java.dev.jna:jna:3.5.2'
}

// Install to another dir
installApp {
    destinationDir = file(installDirRoot + '/fuse')
}

// Add conf dir to classpath of start-up scripts
startScripts {
    applicationName = 'seccloudfs'
    classpath += files('src/dist/conf')
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\conf', '%APP_HOME%\\conf')
        unixScriptFile.text = unixScriptFile.text.replace('$APP_HOME/lib/conf', '$APP_HOME/conf')
    }
}